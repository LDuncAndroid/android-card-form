apply plugin: 'com.android.library'

apply plugin: 'digital.wup.android-maven-publish'

version '4.2.1'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 45
        versionName version

        consumerProguardFiles 'proguard.cfg'
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'io.card:android-sdk:5.5.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.mockito:mockito-core:2.8.9'
}

/* maven release */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
        }
    }
}

publishing {
    publications {
        android.libraryVariants.all { variant ->
            // only publish release variants
            if (variant.name.contains("release")) {
                "maven${variant.name.capitalize()}Aar"(MavenPublication) {
                    from components.findByName("android${variant.name.capitalize()}")
                    groupId 'com.comcarde'
                    artifactId 'cardform'
                    version "$version"
                }
            }
        }
    }

    // This is for publishing Repo + Credentials to CloudRepo
    repositories {
        maven {
            url "https://comcarde.mycloudrepo.io/repositories/comcarde"
            credentials {
                username System.getenv("CLOUDREPO_WRITE_USERNAME") ?: "${cloudrepoWriteUsername}"
                password System.getenv("CLOUDREPO_WRITE_PASSWORD") ?: "${cloudrepoWritePassword}"
            }
        }
    }
}